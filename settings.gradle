import org.gradle.internal.os.OperatingSystem

pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        String frcYear = '2024'
        File frcHome
        if (OperatingSystem.current().isWindows()) {
            String publicFolder = System.getenv('PUBLIC')
            if (publicFolder == null) {
                publicFolder = "C:\\Users\\Public"
            }
            def homeRoot = new File(publicFolder, "wpilib")
            frcHome = new File(homeRoot, frcYear)
        } else {
            def userFolder = System.getProperty("user.home")
            def homeRoot = new File(userFolder, "wpilib")
            frcHome = new File(homeRoot, frcYear)
        }
        def frcHomeMaven = new File(frcHome, 'maven')
        maven {
            name 'frcHome'
            url frcHomeMaven
        }
        maven {
            url "https://jitpack.io"
        }
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.toString().startsWith("com.github.")) {
                var spl = requested.id.toString().split("\\.")
                var user = spl[2]
                var name = spl[3]
                var version = requested.version
                if(version == null) {
                    useModule("com.github.$user:plugin:unspecified")
                } else {
                    useModule("com.github.$user.$name:plugin:$version")
                }
            }
        }
    }
}

Properties props = System.getProperties();
props.setProperty("org.gradle.internal.native.headers.unresolved.dependencies.ignore", "true");
